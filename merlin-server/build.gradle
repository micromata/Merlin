description = 'merlin-server'

buildscript {
    ext.kotlin_version = '1.6.10'
    repositories {
        mavenCentral()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
dependencies {
    implementation project(':merlin-core')
    implementation (libs.commons.cli)
    implementation (libs.commons.io)
    implementation (libs.commons.collections4)
    implementation (libs.commons.text)
    implementation (libs.jackson.databind)
    implementation (libs.jackson.annotations)
    implementation (libs.poi)
    implementation (libs.slf4j.log4j12)

    implementation (libs.jetty.server)
    implementation (libs.jetty.servlet)
    implementation (libs.jetty.servlets)
    implementation (libs.jaxb.core)
    implementation (libs.jaxb.runtime)
    implementation (libs.jersey.container.servlet)
    implementation (libs.jersey.media.multipart)
    implementation (libs.jersey.media.json.jackson)
    implementation (libs.jersey.hk2)
    implementation (libs.jaxb.api)
    implementation (libs.jaxws.api)

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testImplementation (libs.mockito.core)
}

apply plugin: 'application'
apply plugin: 'kotlin'
mainClassName = "de.micromata.merlin.server.Main"

run() {
    doFirst {
        jvmArgs = [
                "-DapplicationHome=${rootDir}"
        ]
    }
}

run.dependsOn ':merlin-webapp:npmBuild'
//run.dependsOn ':merlin-docs:buildWebDoc'

apply plugin: 'distribution'

task createVersionProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['name'] = project.name
            p['build.date.millis'] = '' + System.currentTimeMillis()
            p.store w, null
        }
    }
}

classes {
    dependsOn createVersionProperties
}

// Ugly work arround for getting the applications home dir:
applicationDefaultJvmArgs = ["-DapplicationHome=MY_APPLICATION_HOME"]

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('MY_APPLICATION_HOME', '\$APP_HOME')
        windowsScript.text = windowsScript.text.replace('MY_APPLICATION_HOME', '%~dp0..')
    }
}

// Builds the distribution
distributions {
    main {
        contents {
            // Prepared by nbmBuild:
            from ("${project(':merlin-webapp').projectDir}/build") {
                into 'web'
            }
            // Containing test templates and other stuff:
            from ("${rootProject.projectDir}/examples") {
                into 'examples'
            }
        }
    }
}

distZip.dependsOn ':merlin-webapp:npmBuild'
//distZip.dependsOn ':merlin-docs:buildWebDoc'
task(dist).dependsOn distZip
repositories {
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    mavenCentral()
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

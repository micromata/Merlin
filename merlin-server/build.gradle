description = 'merlin-server'

buildscript {
    ext.kotlin_version = '1.6.10'
    repositories {
        mavenCentral()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

dependencies {
    implementation project(':merlin-core')
    // https://mvnrepository.com/artifact/org.apache.commons/commons-text
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.6'
    implementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.42.v20210604'
    implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.42.v20210604'
    implementation group: 'org.eclipse.jetty', name: 'jetty-servlets', version: '9.4.42.v20210604'
    implementation group: 'org.glassfish.jaxb', name: 'jaxb-core', version: '2.3.0.1'
    implementation group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.1'
    implementation group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: '2.27'
    implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: '2.27'
    implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: '2.27'
    implementation group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: '2.27'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.10.1'
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    implementation group: 'javax.xml.ws', name: 'jaxws-api', version: '2.3.1'
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'

    // https://mvnrepository.com/artifact/commons-cli/commons-cli
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.21.0'
}

apply plugin: 'application'
apply plugin: 'kotlin'
mainClassName = "de.micromata.merlin.server.Main"

run() {
    doFirst {
        jvmArgs = [
                "-DapplicationHome=${rootDir}"
        ]
    }
}

run.dependsOn ':merlin-webapp:npmBuild'
run.dependsOn ':merlin-docs:buildWebDoc'

apply plugin: 'distribution'

task createVersionProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['name'] = project.name
            p['build.date.millis'] = '' + System.currentTimeMillis()
            p.store w, null
        }
    }
}

classes {
    dependsOn createVersionProperties
}

// Ugly work arround for getting the applications home dir:
applicationDefaultJvmArgs = ["-DapplicationHome=MY_APPLICATION_HOME"]

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('MY_APPLICATION_HOME', '\$APP_HOME')
        windowsScript.text = windowsScript.text.replace('MY_APPLICATION_HOME', '%~dp0..')
    }
}

// Builds the distribution
distributions {
    main {
        contents {
            // Prepared by nbmBuild:
            from ("${project(':merlin-webapp').projectDir}/build") {
                into 'web'
            }
            // Containing test templates and other stuff:
            from ("${rootProject.projectDir}/examples") {
                into 'examples'
            }
        }
    }
}

distZip.dependsOn ':merlin-webapp:npmBuild'
distZip.dependsOn ':merlin-docs:buildWebDoc'
task(dist).dependsOn distZip
repositories {
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    mavenCentral()
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
